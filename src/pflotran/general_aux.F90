module General_Aux_module

  implicit none
  
  private 

#include "definitions.h"

  type, public :: general_auxvar_type
    PetscReal, pointer :: pres(:)   ! (iphase)
    PetscReal, pointer :: sat(:)    ! (iphase)
    PetscReal, pointer :: den(:)    ! (iphase)
    PetscReal, pointer :: den_kg(:) ! (iphase)
    PetscReal :: temp
    PetscReal, pointer :: xmol(:,:) ! (icomp,iphase)
    PetscReal, pointer :: H(:) ! MJ/kmol
    PetscReal, pointer :: U(:) ! MJ/kmol
!    PetscReal, pointer :: dsat_dp(:,:)
!    PetscReal, pointer :: dden_dp(:,:)
!    PetscReal, pointer :: dsat_dt(:)
!    PetscReal, pointer :: dden_dt(:)
    PetscReal, pointer :: kvr(:)
    PetscReal :: pert
!    PetscReal, pointer :: dkvr_dp(:)
  end type general_auxvar_type
  
  type, public :: general_parameter_type
    PetscReal, pointer :: diffusion_coefficient(:) ! (iphase)
    PetscReal, pointer :: thermal_conductivity(:) ! (iphase)
  end type general_parameter_type
  
  type, public :: general_type
    PetscInt :: n_zero_rows
    PetscInt, pointer :: zero_rows_local(:), zero_rows_local_ghosted(:)

    PetscBool :: aux_vars_up_to_date
    PetscBool :: inactive_cells_exist
    PetscInt :: num_aux, num_aux_bc
    type(general_parameter_type), pointer :: general_parameter
    type(general_auxvar_type), pointer :: aux_vars(:,:)
    type(general_auxvar_type), pointer :: aux_vars_bc(:)
  end type general_type

  public :: GeneralAuxCreate, GeneralAuxDestroy, &
            GeneralAuxVarCompute, GeneralAuxVarInit, &
            GeneralAuxVarCopy, GeneralAuxVarDestroy

contains


! ************************************************************************** !
!
! GeneralAuxCreate: Allocate and initialize auxilliary object
! author: Glenn Hammond
! date: 03/07/11
!
! ************************************************************************** !
function GeneralAuxCreate(option)

  use Option_module

  implicit none

  type(option_type) :: option
    
  type(general_type), pointer :: GeneralAuxCreate
  
  type(general_type), pointer :: aux

  allocate(aux) 
  aux%aux_vars_up_to_date = PETSC_FALSE
  aux%inactive_cells_exist = PETSC_FALSE
  aux%num_aux = 0
  aux%num_aux_bc = 0
  nullify(aux%aux_vars)
  nullify(aux%aux_vars_bc)
  aux%n_zero_rows = 0
  nullify(aux%zero_rows_local)
  nullify(aux%zero_rows_local_ghosted)

  allocate(aux%general_parameter)
  allocate(aux%general_parameter%diffusion_coefficient(option%nphase))
  aux%general_parameter%diffusion_coefficient = 0.d0
  allocate(aux%general_parameter%thermal_conductivity(option%nphase))
  aux%general_parameter%thermal_conductivity = 0.d0

  GeneralAuxCreate => aux
  
end function GeneralAuxCreate

! ************************************************************************** !
!
! GeneralAuxVarInit: Initialize auxilliary object
! author: Glenn Hammond
! date: 03/07/11
!
! ************************************************************************** !
subroutine GeneralAuxVarInit(aux_var,option)

  use Option_module

  implicit none
  
  type(general_auxvar_type) :: aux_var
  type(option_type) :: option

  allocate(aux_var%pres(option%nphase+THREE_INTEGER))
  aux_var%pres = 0.d0
  allocate(aux_var%sat(option%nphase))
  aux_var%sat = 0.d0
  allocate(aux_var%den(option%nphase))
  aux_var%den = 0.d0
  allocate(aux_var%den_kg(option%nphase))
  aux_var%den_kg = 0.d0
  allocate(aux_var%xmol(option%nflowspec,option%nphase))
  aux_var%xmol = 0.d0
  allocate(aux_var%H(option%nphase))
  aux_var%H = 0.d0
  allocate(aux_var%U(option%nphase))
  aux_var%U = 0.d0
  allocate(aux_var%kvr(option%nphase))
  aux_var%kvr = 0.d0
  
  aux_var%pert = 0.d0
  
end subroutine GeneralAuxVarInit

! ************************************************************************** !
!
! GeneralAuxVarCopy: Copies an auxilliary variable
! author: Glenn Hammond
! date: 03/07/11
!
! ************************************************************************** !  
subroutine GeneralAuxVarCopy(aux_var,aux_var2,option)

  use Option_module

  implicit none
  
  type(general_auxvar_type) :: aux_var, aux_var2
  type(option_type) :: option

  aux_var2%pres = aux_var%pres
  aux_var2%temp = aux_var%temp
  aux_var2%sat = aux_var%sat
  aux_var2%den = aux_var%den
  aux_var2%den_kg = aux_var%den_kg
  aux_var2%xmol = aux_var%xmol
  aux_var2%H = aux_var%H
  aux_var2%U = aux_var%U
  aux_var2%kvr = aux_var%kvr
  aux_var2%pert = aux_var%pert

end subroutine GeneralAuxVarCopy
  
! ************************************************************************** !
!
! GeneralAuxVarCompute: Computes auxilliary variables for each grid cell
! author: Glenn Hammond
! date: 03/07/11
!
! ************************************************************************** !
subroutine GeneralAuxVarCompute(x,gen_aux_var, global_aux_var,&
                                saturation_function,por,perm,option)

  use Option_module
  use Global_Aux_module
  use water_eos_module
  use Gas_Eos_module
  use Saturation_Function_module
  
  implicit none

  type(option_type) :: option
  type(saturation_function_type) :: saturation_function
  PetscReal :: x(option%nflowdof)
  type(general_auxvar_type) :: gen_aux_var
  type(global_auxvar_type) :: global_aux_var

  PetscReal :: por, perm
  PetscInt :: gid, lid, acid, wid, eid
  PetscReal :: den_wat_vap, den_kg_wat_vap, h_wat_vap
  PetscReal :: den_air, h_air
  PetscReal :: den_gp, den_gt, hgp, hgt, dgp, dgt, u
  PetscReal :: krl, visl, dkrl_Se
  PetscReal :: krg, visg, dkrg_Se
  PetscReal :: K_H, Ps
  PetscReal :: guess, dummy
  PetscInt :: apid, cpid, vpid
  PetscErrorCode :: ierr

  lid = option%liquid_phase
  gid = option%gas_phase
  apid = option%air_pressure_id
  cpid = option%capillary_pressure_id
  vpid = option%vapor_pressure_id

  acid = option%air_id ! air component id
  wid = option%water_id
  eid = option%energy_id
  
  gen_aux_var%H = 0.d0
  gen_aux_var%U = 0.d0
  gen_aux_var%kvr = 0.d0
  gen_aux_var%pres = 0.d0
  gen_aux_var%temp = 0.d0
  gen_aux_var%sat = 0.d0
  gen_aux_var%den = 0.d0
  gen_aux_var%den_kg = 0.d0
  gen_aux_var%xmol = 0.d0
  
  select case(global_aux_var%istate)
    case(LIQUID_STATE)
      gen_aux_var%pres(lid) = x(GENERAL_LIQUID_PRESSURE_DOF)
      gen_aux_var%xmol(acid,lid) = x(GENERAL_CONCENTRATION_DOF)
      gen_aux_var%temp = x(GENERAL_TEMPERATURE_DOF)

      gen_aux_var%xmol(wid,lid) = 1.d0 - gen_aux_var%xmol(acid,lid)
      gen_aux_var%sat(lid) = 1.d0
      gen_aux_var%sat(gid) = 0.d0

      call psat(gen_aux_var%temp,Ps,ierr)
      call Henry_air_noderiv(gen_aux_var%pres(lid),gen_aux_var%temp, &
                             Ps,K_H)
      gen_aux_var%pres(apid) = K_H*gen_aux_var%xmol(acid,lid)
      gen_aux_var%pres(vpid) = gen_aux_var%pres(lid) - gen_aux_var%pres(apid)

    case(GAS_STATE)
      gen_aux_var%pres(gid) = x(GENERAL_GAS_PRESSURE_DOF)
      gen_aux_var%pres(apid) = x(GENERAL_AIR_PRESSURE_DOF)
      gen_aux_var%temp = x(GENERAL_TEMPERATURE_DOF)

      gen_aux_var%sat(lid) = 0.d0
      gen_aux_var%sat(gid) = 1.d0
      gen_aux_var%xmol(acid,gid) = gen_aux_var%pres(apid) / gen_aux_var%pres(gid)
      gen_aux_var%xmol(wid,gid) = 1.d0 - gen_aux_var%xmol(acid,gid)
      gen_aux_var%pres(vpid) = gen_aux_var%pres(gid) - gen_aux_var%pres(apid)

    case(TWO_PHASE_STATE)
      gen_aux_var%pres(gid) = x(GENERAL_GAS_PRESSURE_DOF)
      gen_aux_var%pres(apid) = x(GENERAL_AIR_PRESSURE_DOF)
      gen_aux_var%sat(gid) = x(GENERAL_GAS_SATURATION_DOF)
      
      gen_aux_var%sat(lid) = 1.d0 - gen_aux_var%sat(gid)
      gen_aux_var%pres(vpid) = gen_aux_var%pres(gid) - gen_aux_var%pres(apid)
      
      call SatFuncGetCapillaryPressure(gen_aux_var%pres(cpid), &
                                       gen_aux_var%sat(lid), &
                                       saturation_function,option)      

      gen_aux_var%pres(lid) = gen_aux_var%pres(gid) - gen_aux_var%pres(cpid)

      call Henry_air_noderiv(gen_aux_var%pres(lid),gen_aux_var%temp, &
                             gen_aux_var%pres(vpid),K_H)
      gen_aux_var%xmol(acid,lid) = gen_aux_var%pres(apid) / &
                                  (gen_aux_var%pres(gid)*K_H)
      gen_aux_var%xmol(wid,lid) = 1.d0 - gen_aux_var%xmol(apid,lid)
      gen_aux_var%xmol(acid,gid) = gen_aux_var%pres(apid) / gen_aux_var%pres(gid)
      gen_aux_var%xmol(wid,gid) = 1.d0 - gen_aux_var%xmol(acid,gid)

      guess = gen_aux_var%temp
      call Tsat(gen_aux_var%temp,gen_aux_var%pres(vpid),dummy,guess,ierr)

  end select


  if (global_aux_var%istate == LIQUID_STATE .or. &
      global_aux_var%istate == TWO_PHASE_STATE) then
    call wateos_noderiv(gen_aux_var%temp,gen_aux_var%pres(lid), &
                        gen_aux_var%den_kg(lid),gen_aux_var%den(lid), &
                        gen_aux_var%H(lid),option%scale,ierr)

    ! MJ/kmol comp
    gen_aux_var%U(lid) = gen_aux_var%H(lid) - &
                         ! Pa / kmol/m^3 * 1.e-6 = MJ/kmol
                         (gen_aux_var%pres(lid) / gen_aux_var%den(lid) * &
                          option%scale)
                         
    call SatFuncGetRelPermFromSat(gen_aux_var%sat(lid),krl,dkrl_Se, &
                                  saturation_function,lid,PETSC_FALSE,option)
    call visw_noderiv(gen_aux_var%temp,gen_aux_var%pres(lid), &
                      gen_aux_var%pres(vpid),visl,ierr)
    gen_aux_var%kvr(lid) = krl/visl
  endif

  if (global_aux_var%istate == GAS_STATE .or. &
      global_aux_var%istate == TWO_PHASE_STATE) then
    call ideal_gaseos_noderiv(gen_aux_var%pres(apid),gen_aux_var%temp, &
                              option%scale,den_air,h_air,u)
    call steameos(gen_aux_var%temp,gen_aux_var%pres(vpid), &
                  gen_aux_var%pres(apid),den_kg_wat_vap,den_wat_vap,dgp,dgt, &
                  h_wat_vap,hgp,hgt,option%scale,ierr)      
    
    gen_aux_var%den(gid) = den_wat_vap + den_air
    gen_aux_var%den_kg(gid) = den_kg_wat_vap + den_air*FMWAIR
    ! MJ/kmol
    gen_aux_var%H(gid) = gen_aux_var%xmol(wid,gid)*h_wat_vap + &
                         gen_aux_var%xmol(acid,gid)*h_air
    gen_aux_var%U(gid) = gen_aux_var%H(gid) - &
                         ! Pa / kmol/m^3 * 1.e-6 = MJ/kmol
                         (gen_aux_var%pres(gid) / gen_aux_var%den(gid) * &
                          option%scale)

    call SatFuncGetRelPermFromSat(gen_aux_var%sat(gid),krg,dkrg_Se, &
                                  saturation_function,gid,PETSC_FALSE,option)
    call visgas_noderiv(gen_aux_var%temp,gen_aux_var%pres(apid), &
                        gen_aux_var%pres(gid),den_air,visg)
    gen_aux_var%kvr(gid) = krg/visg
  endif

end subroutine GeneralAuxVarCompute

! ************************************************************************** !
!
! GeneralAuxVarDestroy: Deallocates a general auxilliary object
! author: Glenn Hammond
! date: 03/07/11
!
! ************************************************************************** !
subroutine GeneralAuxVarDestroy(aux_var)

  implicit none

  type(general_auxvar_type) :: aux_var
  
  if (associated(aux_var%pres)) deallocate(aux_var%pres)
  nullify(aux_var%pres)
  if (associated(aux_var%sat)) deallocate(aux_var%sat)
  nullify(aux_var%sat)
  if (associated(aux_var%den)) deallocate(aux_var%den)
  nullify(aux_var%den)
  if (associated(aux_var%den_kg)) deallocate(aux_var%den_kg)
  nullify(aux_var%den_kg)
  if (associated(aux_var%xmol)) deallocate(aux_var%xmol)
  nullify(aux_var%xmol)
  if (associated(aux_var%H)) deallocate(aux_var%H)
  nullify(aux_var%H)
  if (associated(aux_var%U)) deallocate(aux_var%U)
  nullify(aux_var%U)
  if (associated(aux_var%kvr)) deallocate(aux_var%kvr)
  nullify(aux_var%kvr)
  
end subroutine GeneralAuxVarDestroy

! ************************************************************************** !
!
! GeneralAuxDestroy: Deallocates a general auxilliary object
! author: Glenn Hammond
! date: 03/07/11
!
! ************************************************************************** !
subroutine GeneralAuxDestroy(aux)

  implicit none

  type(general_type), pointer :: aux
  PetscInt :: iaux, idof
  
  if (.not.associated(aux)) return
  
  if (associated(aux%aux_vars)) then
    do iaux = 1, aux%num_aux
      do idof = 0, size(aux%aux_vars,ONE_INTEGER)-1  ! 0:option%nflowdof
        call GeneralAuxVarDestroy(aux%aux_vars(idof,iaux))
      enddo
    enddo  
    deallocate(aux%aux_vars)
  endif
  nullify(aux%aux_vars)
  if (associated(aux%aux_vars_bc)) then
    do iaux = 1, aux%num_aux_bc
      call GeneralAuxVarDestroy(aux%aux_vars_bc(iaux))
    enddo  
    deallocate(aux%aux_vars_bc)
  endif
  nullify(aux%aux_vars_bc)
  if (associated(aux%zero_rows_local)) deallocate(aux%zero_rows_local)
  nullify(aux%zero_rows_local)
  if (associated(aux%zero_rows_local_ghosted)) deallocate(aux%zero_rows_local_ghosted)
  nullify(aux%zero_rows_local_ghosted)

  if (associated(aux%general_parameter)) then
    if (associated(aux%general_parameter%diffusion_coefficient)) &
      deallocate(aux%general_parameter%diffusion_coefficient)
    nullify(aux%general_parameter%diffusion_coefficient)
    if (associated(aux%general_parameter%thermal_conductivity)) &
      deallocate(aux%general_parameter%thermal_conductivity)
    nullify(aux%general_parameter%thermal_conductivity)
    deallocate(aux%general_parameter)
  endif
  nullify(aux%general_parameter)
  
end subroutine GeneralAuxDestroy

end module General_Aux_module
